plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.github.johnrengelman.shadow'
    id 'io.github.0ffz.github-packages'
}

apply from: 'https://raw.githubusercontent.com/MineInAbyss/shared-gradle/master/common.gradle'

group 'com.derongan.minecraft'
version plugin_version

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'http://www.rutgerkok.nl/repo' }
    maven { url 'http://nexus.okkero.com/repository/maven-releases/' } //Skedule
    maven { url "https://repo.dmulloy2.net/nexus/repository/public/" }
    maven githubPackage.invoke("MineInAbyss/Idofront")
    maven githubPackage.invoke("rutgerkok/BlockLocker")
    maven githubPackage.invoke("MineInAbyss/KotlinSpice")
    maven githubPackage.invoke("MineInAbyss/Geary")
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:$server_version"

    compileOnly platform("com.mineinabyss:kotlinspice:$kotlin_version+")
    compileOnly "com.okkero.skedule:skedule"

    compileOnly 'nl.rutgerkok:blocklocker:1.6.2-SNAPSHOT'
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly "com.comphenix.protocol:ProtocolLib:4.5.0"

    implementation 'com.mineinabyss:idofront:0.5.6'

    testImplementation 'junit:junit:4.12'
}

publishing {
    repositories {
        maven githubPackage.invoke("MineInAbyss/DeeperWorld")
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

shadowJar {
    getArchiveClassifier().set(null)
    relocate 'com.mineinabyss.idofront', "${project.group}.${project.name}.idofront"

    minimize()
}
build.dependsOn shadowJar

compileKotlin { kotlinOptions { jvmTarget = "1.8" } }
compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }
